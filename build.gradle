/**
 * Configures the build script classpath for this project. 
 */
buildscript {
	repositories {
		mavenLocal()
		maven { 
			url 'http://repo.thinkjoy.local/content/groups/public/'
		} 
	}
	dependencies { 
		classpath 'com.atlassian.jgitflow:jgit-flow-core:0.21'
		classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
	}
}

group = 'cn.thinkjoy.gradle.plugins'
version = '1.0.0'
description = "Gradle Gitflow Plugin"

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'


repositories { maven {  url 'http://repo.thinkjoy.local/content/groups/public/'  } }

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'com.atlassian.jgitflow:jgit-flow-core:0.21'
	compile 'org.codehaus.plexus:plexus-components:1.3.1'
	compile 'org.codehaus.plexus:plexus-utils:3.0.17'
	compile 'com.jcraft:jsch.agentproxy.jsch:0.0.7'
	compile 'com.jcraft:jsch.agentproxy.usocket-jna:0.0.7'
	compile 'com.jcraft:jsch.agentproxy.sshagent:0.0.7'
}

publishing {
	publications {
		mavenJava(MavenPublication) { from components.java }
	}

	repositories {
		maven {
			name = 'thinkjoy'
			url "http://repo.thinkjoy.local/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
		}
	}
}

tasks.withType(Compile) { options.encoding = 'UTF-8' }

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}